*** Settings ***

Library         OperatingSystem
Resource        common_resource.txt
Test Setup      Test Case Setup
Test Teardown   Test Case Teardown

*** Keywords ***

Test Case Setup
    Remove Directory  ${WORKSPACE}  recursive
    Create Directory  ${WORKSPACE}
    Set Selenium Timeout  15
    Open Browser  ${MAIN_PAGE}  ${BROWSER}
    Title Should Be  Dippa editor

Test Case Teardown
    Close Browser

Go To Main Page
    Go To  ${MAIN_PAGE}
    Title Should Be  Dippa editor

Create New Demo
    Click Button  demo_button
    Sleep  2s
    ${DEMO_URL} =  Execute Javascript  window.document.location
    [Return]  ${DEMO_URL}

Set Text To Editor  [ARGUMENTS]  ${TEXT}
    Execute Javascript  window.require('app/controller/editor').instance.setValue(${TEXT});

Get Text From Editor
    ${EDITOR_VALUE} =  Execute Javascript  window.require('app/controller/editor').instance.getValue();
    [Return]  ${EDITOR_VALUE}

Wait For Saved
    Wait For Condition  window.$('#save_button').text() === 'Saved!'

Save
    Click Button  save_button  don't wait
    Wait For Saved

Preview
    Click Button  preview_button

Editor Value Should Equal  [ARGUMENTS]  ${TEXT}
    ${EDITOR_VALUE} =  Get Text From Editor
    Should Be Equal As Strings  ${EDITOR_VALUE}  ${TEXT}

Editor Value Should Contain  [ARGUMENTS]  ${TEXT}
    ${EDITOR_VALUE} =  Get Text From Editor
    Should Contain  ${EDITOR_VALUE}  ${TEXT}

Create Github Repository
    ${REPO_NAME} =  Execute Javascript  'dippa_' + (new Date()).getTime()
    Run  grunt create-repository:${REPO_NAME}:${GITHUB_USERNAME}:${GITHUB_PASSWORD}
    ${REPO_URL}  Set Variable  https://github.com/rap1ds-testing/${REPO_NAME}
    [Return]  ${REPO_URL}

Wait For Element Visible  [Arguments]  ${ID}
    Wait For Condition  window.$('#${ID}').is(':visible')

Click Link And Wait Element Visible  [Arguments]  ${BUTTON}  ${WAIT_ELEMENT}
    Click Link  ${BUTTON}  don't wait
    Wait For Element Visible  ${WAIT_ELEMENT}

Click Create New Dippa
    Click Link And Wait Element Visible  create_dippa  step1_done

Click Step One Done
    Click Link And Wait Element Visible  step1_done  step2_done

Click Step Two Done
    Click Link And Wait Element Visible  step2_done  step3_done

Click Step Three Done
    Click Link And Wait Element Visible  step3_done  step4_done

Click Step Four Done
    Click Link  step4_done

Type Github Repository URL  [Arguments]  ${REPO_URL}
    Input Text  repository_url  ${REPO_URL}

Get Dippa Id
    ${ID} =  Execute Javascript  window.location.pathname.split('/')[1]
    [Return]  ${ID}

Get PDF Url
    ${ID} =  Get Dippa Id
    ${PDF_URL}  Set Variable  http://${SERVER}/repositories/${ID}/dippa.pdf
    [Return]  ${PDF_URL}

Download File  [Arguments]  ${URL}
    Run  wget -O ${WORKSPACE}/wget_download ${URL}
    [Return]  ${WORKSPACE}/wget_download

Get PDF Text Content  [Arguments]  ${PDF_FILE}
    Run  pdftotext -enc UTF-8 ${PDF_FILE} ${WORKSPACE}/pdftotext_output.txt
    ${TEXT_CONTENT}  Get File  ${WORKSPACE}/pdftotext_output.txt
    [Return]  ${TEXT_CONTENT}

Get Fixture File  [Arguments]  ${FILE}
    ${FIXTURE_FILE}  Set Variable  ${FIXTURES}/${FILE}
    ${FILE_CONTENT}  Get File  ${FIXTURE_FILE}
    [Return]  ${FILE_CONTENT}

Download Preview PDF File
    ${PDF_URL}  Get PDF Url
    ${DOWNLOADED_FILE}  Download File  ${PDF_URL}
    [Return]  ${DOWNLOADED_FILE}

Should PDF Contain  [Arguments]  ${PDF_FILE}  ${EXPECTED_TEXT}
    ${PDF_TEXT}  Get PDF Text Content  ${PDF_FILE}
    Should Contain  ${PDF_TEXT}  ${EXPECTED_TEXT}

Should Save Button Be Enabled
    Wait For Condition  !(window.$('#save_button').attr('disabled'))

Should Save Button Be Disabled
    Wait For Condition  !!(window.$('#save_button').attr('disabled'))

Open Demo
    ${DEMO_URL} =  Create New Demo
    Set Suite Variable  ${DEMO_URL}

Create Dippa
    Go To Main Page
    ${REPO_URL}  Create Github Repository
    Set Suite Variable  ${REPO_URL}
    Sleep  1s
    Click Create New Dippa
    Click Step One Done
    Type Github Repository URL  ${REPO_URL}
    Click Step Two Done
    Click Step Three Done
    Click Step Four Done

Save Dippa
    Sleep  1s
    ${SAMPLE_JS_CONTENT} =  Get Fixture File  sample_dippa_content.js.tex
    Set Text To Editor  ${SAMPLE_JS_CONTENT}
    Save
    Reload Page
    Sleep  2s
    Editor Value Should Contain  Lorem ipsum dolor sit amet, consectetur adipiscing elit.

PDF Preview
    Sleep  1s
    ${PDF_FILE}  Download Preview PDF File
    Should PDF Contain  ${PDF_FILE}  Lorem ipsum dolor sit amet, consectetur adipiscing elit.

Save To Github
    Sleep  5s
    Run  git clone ${REPO_URL} ${WORKSPACE}/github_repository
    ${DIPPA_TEX}  Get File  ${WORKSPACE}/github_repository/dippa.tex
    Should Contain  ${DIPPA_TEX}  Lorem ipsum dolor sit amet, consectetur adipiscing elit.

Change To References Tab
    Click Element  tab_ref  don't wait
    Sleep  1s

Test Save Enable
    Should Save Button Be Disabled
    Set Text To Editor  "Jee Jee"
    Should Save Button Be Enabled
    Save
    Should Save Button Be Disabled
    Change To References Tab
    Should Save Button Be Disabled

*** Test Cases ***

Test Demo
    Open Demo
    Save Dippa
    PDF Preview
    # Test Save Enable

Test Github  [Tags]  github
    Create Dippa
    Save Dippa
    PDF Preview
    Save To Github
    # Test Save Enable